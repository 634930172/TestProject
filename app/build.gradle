apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def static releaseTime() {
    return new Date().format("yyyy-MM-dd_HH-mm-ss", TimeZone.getTimeZone("GMT+8"))
}

android {
    //签名配置
    signingConfigs {
        release {
            keyAlias 'John'
            keyPassword '123456'
            storeFile file('../TestProject_keystore.jks')
            storePassword '123456'
        }
        debug {
            keyAlias 'John'
            keyPassword '123456'
            storeFile file('../TestProject_keystore.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        flavorDimensions "dimen"//3.0多渠道打包必须加上的
        applicationId "com.john.testproject"
        minSdkVersion 15
        targetSdkVersion 23 //只做权限适配 contentUri后面适配
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

        }
        //多渠道打包3.0配置
        applicationVariants.all { variant ->
            variant.outputs.all {
                if (variant.buildType.name == 'debug') {
                    if (variant.productFlavors[0].name == 'app') {
                        outputFileName = "app-debug.apk"
                    } else {
                        outputFileName = "Small_v${defaultConfig.versionName}_${variant.productFlavors[0].name}-debug.apk"
                    }
                } else if (variant.buildType.name == 'release') {
                    if (variant.productFlavors[0].name == 'app') {
                        outputFileName = "app-release.apk"
                    } else {
                        outputFileName = "Small_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}-release.apk"
                    }
                } else {
                    outputFileName = '-other.apk'
                }
            }
        }
    }

    //多渠道打包
    productFlavors {
        xiaomi { dimension "dimen" }
        app { dimension "dimen" }
        //key需要配置
        productFlavors.all {
            flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name, UMENG_KEY: "您的app_key"]
        }
    }

}
// 导入本地lib
repositories {
    flatDir {
        dirs 'libs'
    }
}
//GreenDao
greendao {
    // 指定数据库schema版本号，迁移等操作会用到
    schemaVersion 8
    // 通过gradle插件生成的数据库相关文件的包名，默认为你的entity所在的包名
    daoPackage 'com.john.testproject.greendao.dao'
    // 生成的数据库文件默认目录为：build/generated/source/greendao
    // 自定义生成数据库文件的目录，可以将生成的文件放到java目录中，而不是build中，这样就不用额外的设置资源目录了
    targetGenDir 'src/main/java'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.android.support:appcompat-v7:26.1.0'
    testCompile 'junit:junit:4.12'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'com.trello:rxlifecycle:0.3.0'
    implementation 'com.trello:rxlifecycle-components:0.3.0'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //侧滑控件
    api 'com.yanzhenjie:recyclerview-swipe:1.1.3'
    api 'com.android.support:recyclerview-v7:26.1.0'
    //材料设计
    api 'com.android.support:design:26.1.0'
    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.4-5'
    //LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    //GreenDao
    api 'org.greenrobot:greendao:3.2.0'
}
